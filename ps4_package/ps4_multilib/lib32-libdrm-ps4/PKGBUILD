# Maintainer: TigerClips1 <spongebob1966@proton.me>


_pkgbasename=ps4-libdrm
_pkgname=libdrm
pkgname=lib32-$_pkgbasename
pkgver=2.4.120
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('MIT')
depends=('lib32-libpciaccess' 'lib32-glibc' $_pkgname)
makedepends=('meson')
checkdepends=('lib32-cairo')
source=(https://dri.freedesktop.org/${_pkgname}/${_pkgname}-${pkgver}.tar.xz
        https://raw.githubusercontent.com/Dr4kk3N/ps4-overlay/main/x11-libs/libdrm/files/libdrm-9999.patch
        )
sha512sums=('SKIP'
            'SKIP')


prepare() {
  mv -v ${_pkgname}-${pkgver} ${_pkgbasename}-${pkgver}
  cd $_pkgbasename-$pkgver
  patch -Np1 -i ../libdrm-9999.patch

}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  arch-meson $_pkgbasename-$pkgver build \
    --libdir=/usr/lib32 \
    -Dudev=false \
    -Dman-pages=disabled \
    -Detnaviv=disabled \
    -Dfreedreno=disabled \
    -Dvc4=disabled \
    -Dvalgrind=disabled
  meson compile -C build
}

check() {
  cd build
  meson test --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
